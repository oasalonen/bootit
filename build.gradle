buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RELEASE")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
    }
}

plugins {
    id 'com.palantir.docker' version '0.19.2'
}

group 'bootit'
version '1.0'

ext {
    appName = 'bootit'
    buildNumber = '28'
    containerRegistry = 'ollireg.azurecr.io'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "kotlin-jpa"
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

processResources {
    // This is done to replace placeholders in app properties with gradle props
    // Need to define expandProps and pass them as input props so gradle sees changes properly.
    def expandProps = [appName: appName, version: version, buildNumber: buildNumber]
    inputs.properties(expandProps)
    filesMatching('application*.yml') {
        expand(expandProps)
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-amqp")
    compile("org.springframework.boot:spring-boot-starter-data-redis")
    compile("io.micrometer:micrometer-registry-prometheus:latest.release")
    compile("com.h2database:h2")
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    compile "org.springframework.boot:spring-boot-devtools"

    compile "net.logstash.logback:logstash-logback-encoder:5.0"

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile 'com.tngtech.jgiven:jgiven-junit:0.15.3'
    testCompile("com.tngtech.jgiven:jgiven-spring:0.15.3")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

bootJar {
}

docker {
    name "${containerRegistry}/${project.group}/${appName}:${version}.${buildNumber}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}